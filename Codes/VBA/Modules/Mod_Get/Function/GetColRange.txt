'----------------------------------------------------------------------------------------------------
'2021/09/19 06:13:37
'----------------------------------------------------------------------------------------------------
Public Function GetColRange(ByVal ws As Worksheet, ByVal TargetCol As LongPtr, Optional ByVal FirstRow As LongPtr, Optional ByVal LastRow As LongPtr) As Range
On Error GoTo Err

    With ws

        '行数値 判定
        If FirstRow <= 0 Then FirstRow = 1
        If LastRow > 0 Then
            Set GetColRange = .Range(.Cells(FirstRow, TargetCol), .Cells(LastRow, TargetCol))
            Exit Function
        End If

        '使用範囲最終行 取得
        With .UsedRange
            Dim UsedLastRow As LongPtr
            UsedLastRow = .Rows(1).Row + (.Rows.Count - 1)
        End With

        '使用範囲配列 取得
        Dim UsedRangeArr() As Variant
        With .Range(.Cells(1, TargetCol), .Cells(UsedLastRow, TargetCol))
            '配列 判定
            If IsArray(.Value) Then
                UsedRangeArr = .Value
            Else
                ReDim UsedRangeArr(1 To 1, 1 To 1)
                UsedRangeArr(1, 1) = .Value
            End If
        End With

        '最終行 取得
        Dim TargetLastRow As LongPtr
        For TargetLastRow = UBound(UsedRangeArr, 1) To 1 Step -1
            If Not IsBlank(UsedRangeArr(CLng(TargetLastRow), 1)) Then
                Set GetColRange = .Range(.Cells(FirstRow, TargetCol), .Cells(TargetLastRow, TargetCol))
                Exit Function
            End If
        Next TargetLastRow

    End With

    Exit Function

'エラー処理
Err:

    Call ShowErrMsg(Err.Description, Err.Number)

End Function
'----------------------------------------------------------------------------------------------------