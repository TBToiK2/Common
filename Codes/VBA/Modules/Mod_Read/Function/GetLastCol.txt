'----------------------------------------------------------------------------------------------------
'2022/03/15 01:46:23
'----------------------------------------------------------------------------------------------------
Public Function GetLastCol(ByVal ws As Worksheet, ByVal targetRow As Long) As Long
On Error GoTo Err

    If targetRow < MIN_ROW Or targetRow > MAX_ROW Then
        ShowErrMsg("指定した行は存在しません。")
        Exit Function
    End If

    With ws

        '使用範囲最終列 取得
        With .UsedRange
            Dim usedLastCol As Long
            usedLastCol = .Columns(.Columns.Count).Column
        End With

        '使用範囲配列 取得
        Dim usedRangeArr() As Variant
        With .Range(.Cells(targetRow, 1), .Cells(targetRow, usedLastCol))
            '配列 判定
            If IsArray(.Value) Then
                usedRangeArr = .Value
            Else
                ReDim usedRangeArr(1 To 1, 1 To 1)
                usedRangeArr(1, 1) = .Value
            End If
        End With

        '最終列 取得
        Dim lastCol As Long
        For lastCol = UBound(usedRangeArr, 2) To 1 Step -1
            If Not IsBlank(usedRangeArr(1, CLng(lastCol))) Then
                GetLastCol = lastCol
                Exit Function
            End If
        Next lastCol

    End With

    Exit Function

'エラー処理
Err:

    Call ShowErrMsg(Err.Description, Err.Number)

End Function
'----------------------------------------------------------------------------------------------------