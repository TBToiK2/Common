'----------------------------------------------------------------------------------------------------
'2022/06/17 01:37:16
'----------------------------------------------------------------------------------------------------
'参照設定
'LibraryName:IWshRuntimeLibrary
'LIBID:{F935DC20-1CF0-11D0-ADB9-00C04FD58A0B}
    'ReferenceName:Windows Script Host Object Model
    'FullPath(win32):C:\Windows\SysWOW64\wshom.ocx
    'FullPath(win64):C:\Windows\System32\wshom.ocx
    'Major.Minor:1.0
        'CLSID:{F935DC22-1CF0-11D0-ADB9-00C04FD58A0B}
        'ProgID:WScript.Shell
Public Function GetSelectFileSpec(ByVal filePath As String, Optional ByVal filterDescription As String, Optional ByVal filterExtentions As String, _
                                  Optional ByVal multiSelect As Boolean, Optional ByVal dialogTitle As String) As Variant
On Error GoTo Err

    'ファイルパス 存在確認
    If Not FolderExists(filePath) Then
        Call ShowErrMsg("指定されたファイルパスが存在しません。", title:="GetSelectFileSpec")
        Exit Function
    End If

    'カレントディレクトリ 変更
    Dim WSH As New WshShell
    WSH.CurrentDirectory = filePath

    If IsBlank(filterDescription) Then filterDescription = "すべてのファイル"
    If IsBlank(filterExtentions) Then filterExtentions = "*.*"
    If IsBlank(dialogTitle) Then dialogTitle = "ファイル 選択"

    'ダイアログボックス 設定
    With Application.FileDialog(msoFileDialogFilePicker)
        With .Filters
            Call .Clear
            Call .Add(filterDescription, filterExtentions, 1)
        End With
        .FilterIndex = 1
        .AllowMultiSelect = multiSelect
        .Title = dialogTitle
        .InitialFileName = vbNullString

        'ファイル選択状態 判定
        If .Show = -1 Then
            If multiSelect Then

                Dim selectedArr() As String
                ReDim selectedArr(.SelectedItems.Count - 1)

                Dim indx As Long
                For indx = 1 To .SelectedItems.Count
                    selectedArr(indx - 1) = .SelectedItems(indx)
                Next indx

                GetSelectFileSpec = selectedArr
            Else
                GetSelectFileSpec = .SelectedItems(1)
            End If
        End If

    End With

    Exit Function

'エラー処理
Err:

    Call ShowErrMsg(Err.Description, Err.Number, "GetSelectFileSpec")

End Function
'----------------------------------------------------------------------------------------------------