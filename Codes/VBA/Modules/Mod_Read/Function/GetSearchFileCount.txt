'----------------------------------------------------------------------------------------------------
'2022/06/01 00:29:22
'----------------------------------------------------------------------------------------------------
'参照設定
'LibraryName:Scripting
'LIBID:{420B2830-E718-11CF-893D-00A0C9054228}
    'ReferenceName:Microsoft Scripting Runtime
    'FullPath(win32):C:\Windows\SysWOW64\scrrun.dll
    'FullPath(win64):C:\Windows\System32\scrrun.dll
    'Major.Minor:1.0
        'CLSID:{0D43FE01-F093-11CF-8940-00A0C9054228}
        'ProgID:Scripting.FileSystemObject
Public Function GetSearchFileCount(ByVal folderSpec As String, ByVal searchFileName As String, Optional ByVal enFileNameType As EnumFileNameType) As Long
On Error GoTo Err

    Dim FSO As New FileSystemObject
    With FSO

        Dim targetFiles As Files
        Set targetFiles = .GetFolder(folderSpec).Files

        Dim searchFileBaseName As String, searchFileExtensionName As String
        searchFileBaseName = .GetBaseName(searchFileName)
        searchFileExtensionName = .GetExtensionName(searchFileName)

        '検索ファイル数 カウント
        Dim targetFile As File
        For Each targetFile In targetFiles

            Dim fileName As String, baseName As String, extensionName As String
            Dim fileCount As Long
            Select Case enFileNameType
                'ファイル名
                Case enFileName
                    fileName = targetFile.Name
                    If searchFileName = fileName Then fileCount = fileCount + 1

                'ベース名
                Case enBaseName
                    baseName = .GetBaseName(targetFile.Name)
                    If searchFileBaseName = baseName Then fileCount = fileCount + 1

                '拡張子名
                Case enExtensionName
                    extensionName = .GetExtensionName(targetFile.Name)
                    If searchFileExtensionName = extensionName Then fileCount = fileCount + 1

            End Select

        Next targetFile

    End With

    GetSearchFileCount = fileCount

    Exit Function

'エラー処理
Err:

    Call ShowErrMsg(Err.Description, Err.Number, "GetSearchFileCount")

End Function
'----------------------------------------------------------------------------------------------------